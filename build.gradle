plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.6'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'com.diffplug.spotless' version '6.12.0'
	id 'org.unbroken-dome.test-sets' version '4.0.0'
}

group = 'com.epam.cloudx.aws'
// TODO this version is not used in the build/deployment process
// TODO instead the commit ID is used to simplify CI/CD
// TODO find way to read this value and use it
version = '1.0'
sourceCompatibility = JavaVersion.VERSION_11

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

jar { //disables '.plain' jar generation
	enabled = false
}

bootJar { // dont include version in jar file name
	project.version = ""
}

ext {
	awsSdkVersion = '2.19.4'
	awsLegacySdkVersion = '1.12.376'
	mapstructVersion = '1.5.3.Final'
	testcontainersVersion = '1.17.6'
	apacheCommonsVersion = '3.12.0'
	memcachedClientVersion = '2.12.3'
}

dependencies {
	//Spring Boot
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	//AWS SDK
	implementation platform('software.amazon.awssdk:bom:' + awsSdkVersion)
	implementation 'com.amazonaws:aws-java-sdk-core:' + awsLegacySdkVersion
	implementation 'software.amazon.awssdk:s3'
	implementation 'software.amazon.awssdk:dynamodb-enhanced'
	implementation 'software.amazon.awssdk:regions'
	implementation 'software.amazon.awssdk:sns'

	//Cache
	implementation 'net.spy:spymemcached:' + memcachedClientVersion

	//Others
	implementation 'org.mapstruct:mapstruct:' + mapstructVersion
	implementation 'org.apache.commons:commons-lang3:' + apacheCommonsVersion

	//Preprocessors
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'org.mapstruct:mapstruct-processor:' + mapstructVersion

	//Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	//Test containers
	testImplementation 'org.testcontainers:testcontainers:' + testcontainersVersion
	testImplementation 'org.testcontainers:junit-jupiter:' + testcontainersVersion
	testImplementation 'org.testcontainers:localstack:' + testcontainersVersion
}

testSets {
	integrationTest
}

spotless {
	java {
		importOrder()
		removeUnusedImports()
	}
}

