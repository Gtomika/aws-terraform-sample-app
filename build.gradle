plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.6'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'com.diffplug.spotless' version '6.12.0'
	id 'jvm-test-suite'
}

group = 'com.epam.cloudx.aws'
version = '1.0'
sourceCompatibility = JavaVersion.VERSION_11

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

jar { //disables '.plain' jar generation
	enabled = false
}

ext {
	awsSdkVersion = '2.19.4'
	mapstructVersion = '1.5.3.Final'
	testcontainersVersion = '1.17.6'
	apacheCommonsVersion = '3.12.0'
}

dependencies {
	//Spring Boot
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	//AWS SDK
	implementation platform('software.amazon.awssdk:bom:' + awsSdkVersion)
	implementation 'com.amazonaws:aws-java-sdk-core:1.12.372'
	implementation 'software.amazon.awssdk:s3'
	implementation 'software.amazon.awssdk:dynamodb-enhanced'
	implementation 'software.amazon.awssdk:ses'

	//Others
	implementation 'org.mapstruct:mapstruct:' + mapstructVersion
	implementation 'org.apache.commons:commons-lang3:' + apacheCommonsVersion

	//Preprocessors
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'org.mapstruct:mapstruct-processor:' + mapstructVersion

	//Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

testing {
	suites {
		//'test' suite is created by default
		integrationTest(JvmTestSuite) {
			dependencies {
				implementation project()
				//Spring Boot
				implementation 'org.springframework.boot:spring-boot-starter-test'
				implementation 'org.springframework.boot:spring-boot-starter'
				implementation 'org.springframework.boot:spring-boot-starter-web'

				//AWS SDK
				implementation 'com.amazonaws:aws-java-sdk-core:1.12.372'
				implementation 'software.amazon.awssdk:s3:' + awsSdkVersion
				implementation 'software.amazon.awssdk:dynamodb-enhanced:' + awsSdkVersion
				implementation 'software.amazon.awssdk:ses:' + awsSdkVersion

				//Others
				implementation 'org.mapstruct:mapstruct:' + mapstructVersion
				implementation 'org.apache.commons:commons-lang3:' + apacheCommonsVersion

				//Preprocessors
				compileOnly 'org.projectlombok:lombok'
				annotationProcessor 'org.projectlombok:lombok'
				annotationProcessor 'org.mapstruct:mapstruct-processor:' + mapstructVersion

				//Test containers
				implementation "org.testcontainers:testcontainers:" + testcontainersVersion
				implementation 'org.testcontainers:junit-jupiter:' + testcontainersVersion
				implementation 'org.testcontainers:localstack:' + testcontainersVersion
			}
			testType = TestSuiteType.INTEGRATION_TEST
		}
	}
}

spotless {
	java {
		importOrder()
		removeUnusedImports()
	}
}

